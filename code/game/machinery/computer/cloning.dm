#define AUTOCLONING_MINIMAL_LEVEL 3

/obj/machinery/computer/cloning
	name = "–∫–æ–Ω—Å–æ–ª—å –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–ª—å–Ω–æ–≥–æ –∞–ø–ø–∞—Ä–∞—Ç–∞"
	desc = "–ò—Å–ø–æ–ª—å–∑—É–µ—Ç—Å—è –¥–ª—è –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è –≥—É–º–∞–Ω–æ–π–¥–æ–≤."
	icon_screen = "dna"
	icon_keyboard = "med_key"
	circuit = /obj/item/circuitboard/computer/cloning
	req_access = list(ACCESS_GENETICS) //for modifying records
	var/obj/machinery/dna_scannernew/scanner //Linked scanner. For scanning.
	var/list/pods //Linked cloning pods
	var/temp = "–ù–ï–ê–ö–¢–ò–í–ù–û"
	var/scantemp_ckey
	var/scantemp = "–ì–û–¢–û–í –ö –°–ö–ê–ù–£"
	var/menu = 1 //Which menu screen to display
	var/list/records = list()
	var/datum/data/record/active_record
	var/obj/item/disk/data/diskette //Incompatible format to genetics machine
	//select which parts of the diskette to load
	var/include_se = FALSE //mutations
	var/include_ui = FALSE //appearance
	var/include_ue = FALSE //blood type, UE, and name

	var/loading = FALSE // Nice loading text
	var/autoprocess = FALSE

	light_color = LIGHT_COLOR_BLUE

/obj/machinery/computer/cloning/Initialize()
	. = ..()
	updatemodules(TRUE)

/obj/machinery/computer/cloning/Destroy()
	if(pods)
		for(var/P in pods)
			DetachCloner(P)
		pods = null
	return ..()

/obj/machinery/computer/cloning/proc/GetAvailablePod(mind = null)
	if(pods)
		for(var/P in pods)
			var/obj/machinery/clonepod/pod = P
			if(pod.occupant && mind && pod.clonemind == mind)
				return null
			if(pod.is_operational && !(pod.occupant || pod.mess))
				return pod

/obj/machinery/computer/cloning/proc/HasEfficientPod()
	if(pods)
		for(var/P in pods)
			var/obj/machinery/clonepod/pod = P
			if(pod.is_operational && pod.efficiency > 5)
				return TRUE

/obj/machinery/computer/cloning/proc/GetAvailableEfficientPod(mind = null)
	if(pods)
		for(var/P in pods)
			var/obj/machinery/clonepod/pod = P
			if(pod.occupant && pod.clonemind == mind)
				return pod
			else if(!. && pod.is_operational && !(pod.occupant || pod.mess) && pod.efficiency > 5)
				. = pod

/proc/grow_clone_from_record(obj/machinery/clonepod/pod, datum/data/record/R, empty)
	return pod.growclone(R.fields["name"], R.fields["UI"], R.fields["SE"], R.fields["mindref"], R.fields["last_death"], R.fields["blood_type"], R.fields["mrace"], R.fields["features"], R.fields["factions"], R.fields["quirks"], R.fields["bank_account"], R.fields["traumas"], empty)

/obj/machinery/computer/cloning/process()
	if(!(scanner && LAZYLEN(pods) && autoprocess))
		return

	if(scanner.occupant && scanner.scan_level > 2)
		scan_occupant(scanner.occupant)

	for(var/datum/data/record/R in records)
		var/obj/machinery/clonepod/pod = GetAvailableEfficientPod(R.fields["mindref"])

		if(!pod)
			return

		if(pod.occupant)
			break

		var/result = grow_clone_from_record(pod, R)
		if(result & CLONING_SUCCESS)
			temp = "[R.fields["name"]] => <font class='good'>–ö–õ–û–ù–ò–†–û–í–ê–ù–ò–ï...</font>"
			log_cloning("Cloning of [key_name(R.fields["mindref"])] automatically started via autoprocess - [src] at [AREACOORD(src)]. Pod: [pod] at [AREACOORD(pod)].")
		if(result & CLONING_DELETE_RECORD)
			records -= R


/obj/machinery/computer/cloning/proc/updatemodules(findfirstcloner)
	scanner = findscanner()
	if(findfirstcloner && !LAZYLEN(pods))
		findcloner()
	if(!autoprocess)
		STOP_PROCESSING(SSmachines, src)
	else
		START_PROCESSING(SSmachines, src)

/obj/machinery/computer/cloning/proc/findscanner()
	var/obj/machinery/dna_scannernew/scannerf = null

	// Loop through every direction
	for(var/direction in GLOB.cardinals)

		// Try to find a scanner in that direction
		scannerf = locate(/obj/machinery/dna_scannernew, get_step(src, direction))

		// If found and operational, return the scanner
		if (!isnull(scannerf) && scannerf.is_operational)
			return scannerf

	// If no scanner was found, it will return null
	return null

/obj/machinery/computer/cloning/proc/findcloner()
	var/obj/machinery/clonepod/podf = null

	for(var/direction in GLOB.cardinals)

		podf = locate(/obj/machinery/clonepod, get_step(src, direction))
		if (!isnull(podf) && podf.is_operational)
			AttachCloner(podf)

/obj/machinery/computer/cloning/proc/AttachCloner(obj/machinery/clonepod/pod)
	if(!pod.connected)
		pod.connected = src
		LAZYADD(pods, pod)

/obj/machinery/computer/cloning/proc/DetachCloner(obj/machinery/clonepod/pod)
	pod.connected = null
	LAZYREMOVE(pods, pod)

/obj/machinery/computer/cloning/attackby(obj/item/W, mob/user, params)
	if(istype(W, /obj/item/disk/data)) //INSERT SOME DISKETTES
		if (!diskette)
			if (!user.transferItemToLoc(W,src))
				return
			diskette = W
			to_chat(user, "<span class='notice'>–í—Å—Ç–∞–≤–ª—è—é [W].</span>")
			playsound(src, 'sound/machines/terminal_insert_disc.ogg', 50, FALSE)
			updateUsrDialog()
	else if(W.tool_behaviour == TOOL_MULTITOOL)
		if(!multitool_check_buffer(user, W))
			return
		var/obj/item/multitool/P = W

		if(istype(P.buffer, /obj/machinery/clonepod))
			if(get_area(P.buffer) != get_area(src))
				to_chat(user, "<font color = #666633>-% –ù–µ–≤–æ–∑–º–æ–∂–Ω–æ —Å–≤—è–∑–∞—Ç—å –º–∞—à–∏–Ω—ã –º–µ–∂–¥—É –∑–æ–Ω–∞–º–∏ –ø–∏—Ç–∞–Ω–∏—è. –ë—É—Ñ–µ—Ä –æ—á–∏—â–µ–Ω %-</font color>")
				P.buffer = null
				return
			to_chat(user, "<font color = #666633>-% –°–≤—è–∑—ã–≤–∞—é [P.buffer] —Å [src] %-</font color>")
			var/obj/machinery/clonepod/pod = P.buffer
			if(pod.connected)
				pod.connected.DetachCloner(pod)
			AttachCloner(pod)
		else
			P.buffer = src
			to_chat(user, "<font color = #666633>-% –ó–∞–ø–∏—Å—ã–≤–∞—é –¥–∞–Ω–Ω—ã–µ [REF(P.buffer)] [P.buffer.name] –≤ –±—É—Ñ–µ—Ä %-</font color>")
		return
	else
		return ..()

/obj/machinery/computer/cloning/ui_interact(mob/user)
	. = ..()

	updatemodules(TRUE)

	var/dat = ""
	dat += "<meta charset='utf-8'>"
	dat += "<a href='byond://?src=[REF(src)];refresh=1'>üîÅ</a>"

	if(scanner && HasEfficientPod() && scanner.scan_level >= AUTOCLONING_MINIMAL_LEVEL)
		if(!autoprocess)
			dat += "<a href='byond://?src=[REF(src)];task=autoprocess'>–ê–í–¢–û</a>"
		else
			dat += "<a href='byond://?src=[REF(src)];task=stopautoprocess'>–û–°–¢–ê–ù–û–í–ò–¢–¨ –ê–í–¢–û</a>"
	else
		dat += "<span class='linkOff'>–ê–í–¢–û</span>"
	dat += "<h3>–°—Ç–∞—Ç—É—Å</h3>"
	dat += "<div class='statusDisplay'>[temp]&nbsp;</div>"
	switch(menu)
		if(1)
			// Modules
			if (isnull(scanner) || !LAZYLEN(pods))
				dat += "<h3>–ú–æ–¥—É–ª–∏</h3>"
				//dat += "<a href='byond://?src=[REF(src)];relmodules=1'>Reload Modules</a>"
				if (isnull(scanner))
					dat += "<font class='bad'>–û–®–ò–ë–ö–ê: –°–ö–ê–ù –ù–ï –û–ë–ù–ê–†–£–ñ–ï–ù!</font><br>"
				if (!LAZYLEN(pods))
					dat += "<font class='bad'>–û–®–ò–ë–ö–ê: –ö–ê–ú–ï–†–ê –ö–õ–û–ù–ò–†–û–í–ê–ù–ò–Ø –ù–ï –û–ë–ù–ê–†–£–ñ–ï–ù–ê</font><br>"

			// Scanner
			if (!isnull(scanner))
				var/mob/living/scanner_occupant = get_mob_or_brainmob(scanner.occupant)

				dat += "<h3>–°–∫–∞–Ω–µ—Ä</h3>"

				dat += "<div class='statusDisplay'>"
				if(!scanner_occupant)
					dat += "–°–∫–∞–Ω–µ—Ä –Ω–µ –∑–∞–Ω—è—Ç"
				else if(loading)
					dat += "[scanner_occupant] => –°–∫–∞–Ω–∏—Ä—É—é..."
				else
					if(scanner_occupant.ckey != scantemp_ckey)
						scantemp = "–ì–û–¢–û–í –ö –°–ö–ê–ù–£"
						scantemp_ckey = scanner_occupant.ckey
					dat += "[scanner_occupant] => [scantemp]"
				dat += "</div>"

				if(scanner_occupant)
					dat += "<a href='byond://?src=[REF(src)];scan=1'>–ù–∞—á–∞—Ç—å</a>"
					dat += "<a href='byond://?src=[REF(src)];scan=1;body_only=1'>–°–∫–∞–Ω–∏—Ä–æ–≤–∞—Ç—å —Ç–µ–ª–æ</a>"
					dat += "<br><a href='byond://?src=[REF(src)];lock=1'>[scanner.locked ? "–†–∞–±–ª–æ–∫–∏—Ä–æ–≤–∞—Ç—å" : "–ó–∞–±–ª–æ–∫–∏—Ä–æ–≤–∞—Ç—å"] —Å–∫–∞–Ω–µ—Ä</a>"
				else
					dat += "<span class='linkOff'>–ù–∞—á–∞—Ç—å —Å–∫–∞–Ω–∏—Ä–æ–≤–∞–Ω–∏–µ</span>"

			// Database
			dat += "<h3>Database Functions</h3>"
			if (records.len && records.len > 0)
				dat += "<a href='byond://?src=[REF(src)];menu=2'>–ó–∞–ø–∏—Å–∏ ([records.len])</a><br>"
			else
				dat += "<span class='linkOff'>–ó–∞–ø–∏—Å–∏ (0)</span><br>"
			if (diskette)
				dat += "<a href='byond://?src=[REF(src)];disk=eject'>–í—ã—Ç–∞—â–∏—Ç—å –¥–∏—Å–∫</a><br>"



		if(2)
			dat += "<h3>Current records</h3>"
			dat += "<a href='byond://?src=[REF(src)];menu=1'><< –ù–∞–∑–∞–¥</a><br><br>"
			for(var/datum/data/record/R in records)
				dat += "<h4>[R.fields["name"]]</h4>ID –°–∫–∞–Ω–∏—Ä–æ–≤–∞–Ω–∏—è [R.fields["id"]] <a href='byond://?src=[REF(src)];view_rec=[R.fields["id"]]'>–ó–∞–ø–∏—Å–∏</a>"
		if(3)
			dat += "<h3>–í—ã–±—Ä–∞–Ω–Ω–∞—è –∑–∞–ø–∏—Å—å</h3>"
			dat += "<a href='byond://?src=[REF(src)];menu=2'><< –ù–∞–∑–∞–¥</a><br>"

			if (!active_record)
				dat += "<font class='bad'>–ó–∞–ø–∏—Å—å –Ω–µ –æ–±–Ω–∞—Ä—É–∂–µ–Ω–∞.</font>"
			else
				var/body_only = active_record.fields["body_only"]
				dat += "<h4>[active_record.fields["name"]][body_only ? " - –¢–û–õ–¨–ö–û-–¢–ï–õ–û" : ""]</h4>"
				dat += "ID –°–∫–∞–Ω–∏—Ä–æ–≤–∞–Ω–∏—è [active_record.fields["id"]] \
					[!body_only ? "<a href='byond://?src=[REF(src)];clone=[active_record.fields["id"]]'>–ö–ª–æ–Ω–∏—Ä–æ–≤–∞—Ç—å</a>" : "" ]\
					<a href='byond://?src=[REF(src)];clone=[active_record.fields["id"]];empty=TRUE'>–û—á–∏—Å—Ç–∏—Ç—å</a><br>"

				var/obj/item/implant/health/H = locate(active_record.fields["imp"])

				if ((H) && (istype(H)))
					dat += "<b>–ò–º–ø–ª–∞–Ω—Ç –∂–∏–∑–Ω–µ–¥–µ—è—Ç–µ–ª—å–Ω–æ—Å—Ç–∏:</b><br />[H.sensehealth()]<br><br />"
				else
					dat += "<font class='bad'>–ò–º–ø–ª–∞–Ω—Ç –∂–∏–∑–Ω–µ–¥–µ—è—Ç–µ–ª—å–Ω–æ—Å—Ç–∏ –Ω–µ –æ–±–Ω–∞—Ä—É–∂–µ–Ω.</font><br /><br />"

				dat += "<b>Unique Identifier:</b><br /><span class='highlight'>[active_record.fields["UI"]]</span><br>"
				dat += "<b>–°—Ç—Ä—É–∫—Ç—É—Ä–Ω—ã–µ —ç–Ω–∑–∏–º—ã:</b><br /><span class='highlight'>"
				for(var/key in active_record.fields["SE"])
					if(key != RACEMUT)
						var/val = active_record.fields["SE"][key]
						var/alias = GLOB.all_mutations[key].alias
						dat +="[alias]: [val]<br />"

				dat += "</span><br />"

				if(diskette && diskette.fields)
					dat += "<div class='block'>"
					dat += "<h4>–î–∏—Å–∫</h4>"
					dat += "<b>–°–æ–¥–µ—Ä–∂–∞–Ω–∏–µ:</b> "
					var/list/L = list()
					if(diskette.fields["UI"])
						L += "Unique Identifier"
					if(diskette.fields["UE"] && diskette.fields["name"] && diskette.fields["blood_type"])
						L += "Unique Enzymes"
					if(diskette.fields["SE"])
						L += "Structural Enzymes"
					dat += english_list(L, "Empty", " + ", " + ")
					var/can_load = FALSE
					var/obj/item/card/id/C = user.get_idcard(TRUE)
					if(C)
						if(check_access(C))
							can_load = TRUE
					if(can_load)
						dat += "<br /><a href='byond://?src=[REF(src)];disk=load'>–ó–∞–≥—Ä—É–∑–∏—Ç—å —Å –¥–∏—Å–∫–∞</a>"
					else
						dat += "<span class='linkOff'>–û—à–∏–±–∫–∞ –∑–∞–≥—Ä—É–∑–∫–∏ —Å –¥–∏—Å–∫–∞: –î–æ—Å—Ç—É–ø –∑–∞–ø—Ä–µ—â—ë–Ω</span>"
					if(diskette.fields["SE"])
						if(!include_se)
							dat += "<br /><a href='byond://?src=[REF(src)];task=include_se'>Currently Excluding SE</a>"
						else
							dat += "<br /><a href='byond://?src=[REF(src)];task=exclude_se'>Currently Including SE</a>"
					if(diskette.fields["UI"])
						if(!include_ui)
							dat += "<br /><a href='byond://?src=[REF(src)];task=include_ui'>Currently Excluding UI</a>"
						else
							dat += "<br /><a href='byond://?src=[REF(src)];task=exclude_ui'>Currently Including UI</a>"
					if(diskette.fields["UE"])
						if(!include_ue)
							dat += "<br /><a href='byond://?src=[REF(src)];task=include_ue'>Currently Excluding UE</a>"
						else
							dat += "<br /><a href='byond://?src=[REF(src)];task=exclude_ue'>Currently Including UE</a>"


					dat += "<br /><a href='byond://?src=[REF(src)];disk=save'>–°–æ—Ö—Ä–∞–Ω–∏—Ç—å –Ω–∞ –¥–∏—Å–∫</a>"
					dat += "</div>"

				dat += "<font size=1><a href='byond://?src=[REF(src)];del_rec=1'>–£–¥–∞–ª–∏—Ç—å</a></font>"

		if(4)
			if (!active_record)
				menu = 2
				ui_interact(user)
				return
			dat += "<b><a href='byond://?src=[REF(src)];del_rec=1'>–ü–æ–¥—Ç–≤–µ—Ä–¥–∏—Ç–µ.</a></b><br>"
			dat += "<b><a href='byond://?src=[REF(src)];menu=3'>–û—Ç–º–µ–Ω–∞</a></b>"

	var/datum/browser/popup = new(user, "cloning", "–°–∏—Å—Ç–µ–º–∞ –∫–æ–Ω—Ç—Ä–æ–ª—è –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è")
	popup.set_content(dat)
	popup.open()

/obj/machinery/computer/cloning/Topic(href, href_list)
	if(..())
		return

	if(loading)
		return

	if(href_list["task"])
		switch(href_list["task"])
			if("autoprocess")
				if(scanner && HasEfficientPod() && scanner.scan_level >= AUTOCLONING_MINIMAL_LEVEL)
					autoprocess = TRUE
					START_PROCESSING(SSmachines, src)
					playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)
			if("stopautoprocess")
				autoprocess = FALSE
				STOP_PROCESSING(SSmachines, src)
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			if("include_se")
				include_se = TRUE
			if("exclude_se")
				include_se = FALSE
			if("include_ui")
				include_ui = TRUE
			if("exclude_ui")
				include_ui = FALSE
			if("include_ue")
				include_ue = TRUE
			if("exclude_ue")
				include_ue = FALSE

	else if ((href_list["scan"]) && !isnull(scanner) && scanner.is_operational)
		scantemp = ""
		var/body_only = href_list["body_only"]
		loading = TRUE
		updateUsrDialog()
		playsound(src, 'sound/machines/terminal_prompt.ogg', 50, FALSE)
		say("–ù–∞—á–∏–Ω–∞—é —Å–∫–∞–Ω–∏—Ä–æ–≤–∞–Ω–∏–µ...")

		addtimer(CALLBACK(src, .proc/do_scan, usr, body_only), 2 SECONDS)

		//No locking an open scanner.
	else if ((href_list["lock"]) && !isnull(scanner) && scanner.is_operational)
		if ((!scanner.locked) && (scanner.occupant))
			scanner.locked = TRUE
			playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
		else
			scanner.locked = FALSE
			playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)

	else if(href_list["view_rec"])
		playsound(src, "terminal_type", 25, FALSE)
		active_record = find_record("id", href_list["view_rec"], records)
		if(active_record)
			menu = 3
		else
			temp = "–ó–ê–ü–ò–°–¨ –ù–ï –ù–ê–ô–î–ï–ù–ê"

	else if (href_list["del_rec"])
		if ((!active_record) || (menu < 3))
			return
		if (menu == 3) //If we are viewing a record, confirm deletion
			var/has_access = FALSE
			if(ishuman(usr))
				var/mob/living/carbon/human/user = usr
				var/obj/item/card/id/C = user.get_idcard(TRUE)
				if(C)
					if(check_access(C))
						has_access = TRUE
			if(active_record.fields["body_only"]) //Body-only scans are not as important and can be deleted freely
				has_access = TRUE
			if(has_access)
				temp = "–£–î–ê–õ–ò–¢–¨?"
				menu = 4
				playsound(src, 'sound/machines/terminal_prompt.ogg', 50, FALSE)
			else
				temp = "–î–û–°–¢–£–ü –ó–ê–ü–†–ï–©–Å–ù"
				menu = 2
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)


		else if (menu == 4)
			log_cloning("[key_name(usr)] deleted [key_name(active_record.fields["mindref"])]'s cloning records from [src] at [AREACOORD(src)].")
			temp = "[active_record.fields["name"]] => Record deleted."
			records.Remove(active_record)
			active_record = null
			playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)
			menu = 2

	else if (href_list["disk"]) //Load or eject.
		switch(href_list["disk"])
			if("load")


				if (!diskette || !istype(diskette.fields))
					temp = "<font class='bad'>–û–®–ò–ë–ö–ê –ó–ê–ì–†–£–ó–ö–ò</font>"
					updateUsrDialog()
					playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
					return
				if (!active_record)
					temp = "<font class='bad'>–û–®–ò–ë–ö–ê –ó–ê–ü–ò–°–ò</font>"
					menu = 1
					updateUsrDialog()
					playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
					return

				if(include_ue)
					overwrite_field_if_available(active_record, diskette, "UE")
					overwrite_field_if_available(active_record, diskette, "name")
					overwrite_field_if_available(active_record, diskette, "blood_type")
				if(include_ui)
					overwrite_field_if_available(active_record, diskette, "UI")
				if(include_se)
					overwrite_field_if_available(active_record, diskette, "SE")

				log_cloning("[key_name(usr)] uploaded [key_name(active_record.fields["mindref"])]'s cloning records to [src] at [AREACOORD(src)] via [diskette].")
				temp = "–ó–ê–ì–†–£–ó–ö–ê –£–°–ü–ï–®–ù–ê"
				playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)

			if("eject")
				if(diskette)
					diskette.forceMove(drop_location())
					diskette = null
					playsound(src, 'sound/machines/terminal_insert_disc.ogg', 50, FALSE)
			if("save")
				if(!diskette || diskette.read_only || !active_record || !active_record.fields)
					temp = "<font class='bad'>Save error.</font>"
					updateUsrDialog()
					playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
					return

				log_cloning("[key_name(usr)] added [key_name(active_record.fields["mindref"])]'s cloning records to [diskette] via [src] at [AREACOORD(src)].")
				diskette.fields = active_record.fields.Copy()
				diskette.name = "data disk - '[diskette.fields["name"]]'"
				temp = "Save successful."
				playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)

	else if (href_list["refresh"])
		updateUsrDialog()
		playsound(src, "terminal_type", 25, FALSE)

	else if (href_list["clone"])
		var/datum/data/record/C = find_record("id", href_list["clone"], records)
		var/empty = href_list["empty"]
		//Look for that player! They better be dead!
		if(C)
			if(C.fields["body_only"] && !empty)
				temp = "<font class='bad'>–ù–µ–≤–æ–∑–º–æ–∂–Ω–æ –∏–Ω–∏—Ü–∏–∏—Ä–æ–≤–∞—Ç—å –æ–±—ã—á–Ω–æ–µ –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏–µ —Å –ø–æ–º–æ—â—å—é —Å–∫–∞–Ω–∏—Ä–æ–≤–∞–Ω–∏—è —Ç–æ–ª—å–∫–æ —Ç–µ–ª–∞.</font>"
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			var/obj/machinery/clonepod/pod = GetAvailablePod()
			var/success = FALSE
			//Can't clone without someone to clone.  Or a pod.  Or if the pod is busy. Or full of gibs.
			if(!LAZYLEN(pods))
				temp = "<font class='bad'>–ö–∞–ø—Å—É–ª–∞ –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è –Ω–µ –Ω–∞–π–¥–µ–Ω–∞.</font>"
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			else if(!pod)
				temp = "<font class='bad'>–ö–∞–ø—Å—É–ª–∞ –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è –Ω–µ–¥–æ—Å—Ç—É–ø–Ω–∞.</font>"
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			else if(!CONFIG_GET(flag/revival_cloning) && !empty)
				temp = "<font class='bad'>–ù–µ —É–¥–∞–ª–æ—Å—å –∑–∞–ø—É—Å—Ç–∏—Ç—å —Ü–∏–∫–ª –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è.</font>"
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			else if(pod.occupant)
				temp = "<font class='bad'>–¶–∏–∫–ª –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è —É–∂–µ –∑–∞–ø—É—â–µ–Ω.</font>"
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			else
				var/result = grow_clone_from_record(pod, C, empty)
				if(result & CLONING_SUCCESS)
					temp = "[C.fields["name"]] => <font class='good'>–í—ã–ø–æ–ª–Ω—è–µ—Ç—Å—è —Ü–∏–∫–ª –∫–ª–æ–Ω–∏—Ä–æ–≤–∞–Ω–∏—è...</font>"
					playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)
					if(active_record == C)
						active_record = null
					menu = 1
					success = TRUE
					if(!empty)
						log_cloning("[key_name(usr)] initiated cloning of [key_name(C.fields["mindref"])] via [src] at [AREACOORD(src)]. Pod: [pod] at [AREACOORD(pod)].")
					else
						log_cloning("[key_name(usr)] initiated EMPTY cloning of [key_name(C.fields["mindref"])] via [src] at [AREACOORD(src)]. Pod: [pod] at [AREACOORD(pod)].")
				if(result &	CLONING_DELETE_RECORD)
					if(active_record == C)
						active_record = null
					menu = 1
					records -= C

			if(!success)
				temp = "[C.fields["name"]] => <font class='bad'>–û—à–∏–±–∫–∞ –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏–∏.</font>"
				playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)

		else
			temp = "<font class='bad'>–î–∞–Ω–Ω—ã–µ –ø–æ–≤—Ä–µ–∂–¥–µ–Ω—ã.</font>"
			playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)

	else if (href_list["menu"])
		menu = text2num(href_list["menu"])
		playsound(src, "terminal_type", 25, FALSE)

	add_fingerprint(usr)
	updateUsrDialog()
	return

/obj/machinery/computer/cloning/proc/do_scan(mob/user, body_only)
	scan_occupant(scanner.occupant, user, body_only)

	loading = FALSE
	updateUsrDialog()
	playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50, FALSE)

/obj/machinery/computer/cloning/proc/scan_occupant(occupant, mob/M, body_only)
	var/mob/living/mob_occupant = get_mob_or_brainmob(occupant)
	var/datum/dna/dna
	var/datum/bank_account/has_bank_account

	// Do not use unless you know what they are.
	var/mob/living/carbon/C = mob_occupant
	var/mob/living/brain/B = mob_occupant

	if(ishuman(mob_occupant))
		dna = C.has_dna()
		var/obj/item/card/id/I = C.get_idcard(TRUE)
		if(I)
			has_bank_account = I.registered_account
	if(isbrain(mob_occupant))
		dna = B.stored_dna

	if(!istype(dna))
		scantemp = "<font class='bad'>–ù–µ —É–¥–∞–ª–æ—Å—å –Ω–∞–π—Ç–∏ –¥–æ—Å—Ç–æ–≤–µ—Ä–Ω—ã–µ –≥–µ–Ω–µ—Ç–∏—á–µ—Å–∫–∏–µ –¥–∞–Ω–Ω—ã–µ.</font>"
		playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
		return
	if(!body_only && (mob_occupant.suiciding || mob_occupant.hellbound))
		scantemp = "<font class='bad'>–ú–æ–∑–≥ —Å—É–±—ä–µ–∫—Ç–∞ –Ω–µ —Ä–µ–∞–≥–∏—Ä—É–µ—Ç –Ω–∞ —Å–∫–∞–Ω–∏—Ä—É—é—â–∏–µ —Å—Ç–∏–º—É–ª—ã.</font>"
		playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
		return
	if((HAS_TRAIT(mob_occupant, TRAIT_HUSK)) && (src.scanner.scan_level < 2))
		scantemp = "<font class='bad'>–¢–µ–ª–æ —Å—É–±—ä–µ–∫—Ç–∞ —Å–ª–∏—à–∫–æ–º –ø–æ–≤—Ä–µ–∂–¥–µ–Ω–æ –¥–ª—è –Ω–æ—Ä–º–∞–ª—å–Ω–æ–≥–æ —Å–∫–∞–Ω–∏—Ä–æ–≤–∞–Ω–∏—è.</font>"
		playsound(src, 'sound/machines/terminal_alert.ogg', 50, FALSE)
		return
	if(HAS_TRAIT(mob_occupant, TRAIT_BADDNA))
		scantemp = "<font class='bad'>–î–ù–ö —Å—É–±—ä–µ–∫—Ç–∞ –ø–æ–≤—Ä–µ–∂–¥–µ–Ω–∞ –±–µ–∑ –≤—Å—è–∫–æ–π –Ω–∞–¥–µ–∂–¥—ã –Ω–∞ –≤–æ—Å—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–∏–µ.</font>"
		playsound(src, 'sound/machines/terminal_alert.ogg', 50, FALSE)
		return
	if (!body_only && isnull(mob_occupant.mind))
		scantemp = "<font class='bad'>–°–±–æ–π –º–µ–Ω—Ç–∞–ª—å–Ω–æ–≥–æ –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å–∞.</font>"
		playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
		return
	var/datum/data/record/R = new()
	if(dna.species)
		if(NO_DNA_COPY in dna.species.species_traits)
			scantemp = "<font class='bad'>–ù–µ–≤–æ–∑–º–æ–∂–Ω–æ –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ—Å—Ç–∏ –≥–µ–Ω–µ—Ç–∏—á–µ—Å–∫–∏–µ –¥–∞–Ω–Ω—ã–µ.</font>"
			playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 50, FALSE)
			return
		// We store the instance rather than the path, because some
		// species (abductors, slimepeople) store state in their
		// species datums
		dna.delete_species = FALSE
		R.fields["mrace"] = dna.species
	else
		var/datum/species/rando_race = pick(GLOB.roundstart_races)
		R.fields["mrace"] = rando_race.type

	R.fields["name"] = mob_occupant.real_name
	R.fields["id"] = copytext_char(md5(mob_occupant.real_name), 2, 6)
	R.fields["UE"] = dna.unique_enzymes
	R.fields["UI"] = dna.uni_identity
	R.fields["SE"] = dna.mutation_index
	R.fields["blood_type"] = dna.blood_type
	R.fields["features"] = dna.features
	R.fields["factions"] = mob_occupant.faction
	R.fields["quirks"] = list()
	for(var/V in mob_occupant.roundstart_quirks)
		var/datum/quirk/T = V
		R.fields["quirks"][T.type] = T.clone_data()

	R.fields["traumas"] = list()
	if(ishuman(mob_occupant))
		R.fields["traumas"] = C.get_traumas()
	if(isbrain(mob_occupant))
		R.fields["traumas"] = B.get_traumas()

	R.fields["bank_account"] = has_bank_account
	R.fields["mindref"] = "[REF(mob_occupant.mind)]"
	R.fields["last_death"] = mob_occupant.stat == DEAD && mob_occupant.mind ? mob_occupant.mind.last_death : -1
	R.fields["body_only"] = body_only

	if(!body_only)
		//Add an implant if needed
		var/obj/item/implant/health/imp
		for(var/obj/item/implant/health/HI in mob_occupant.implants)
			imp = HI
			break
		if(!imp)
			imp = new /obj/item/implant/health(mob_occupant)
			imp.implant(mob_occupant)
		R.fields["imp"] = "[REF(imp)]"

	var/datum/data/record/old_record = find_record("mindref", REF(mob_occupant.mind), records)
	if(body_only)
		old_record = find_record("UE", dna.unique_enzymes, records) //Body-only records cannot be identified by mind, so we use the DNA
		if(old_record && ((old_record.fields["UI"] != dna.uni_identity) || (!old_record.fields["body_only"]))) //Never overwrite a mind-and-body record if it exists
			old_record = null
	if(old_record)
		records -= old_record
		scantemp = "–ó–ê–ü–ò–°–¨ –û–ë–ù–û–í–õ–ï–ù–ê"
	else
		scantemp = "–°–£–ë–¨–ï–ö–¢ –£–°–ü–ï–®–ù–û –ü–†–û–°–ö–ê–ù–ò–†–û–í–ê–ù"
	records += R
	log_cloning("[M ? key_name(M) : "Autoprocess"] added the [body_only ? "body-only " : ""]record of [key_name(mob_occupant)] to [src] at [AREACOORD(src)].")
	playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 50)
